plugins {
    id "com.google.protobuf" version "0.8.8"
    id "groovy"
    id "codenarc"
}

sourceCompatibility = 1.8
version = '1.0'

codenarc.configFile = file("./codenarc-rules.groovy")
codenarc.reportFormat = 'text'

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

def grpcVersion = '1.21.0'
def protocVersion = '3.6.1'
def protobufVersion = '3.6.1'
def nettyTcNativeVersion = '2.0.25.Final'

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {
                outputSubDir = "java"
            }
        }

        // Add each output source directory to the sourceSet based on its basename (e.g., `java`).
        all().each { task ->
            task.outputSourceDirectorySet.srcDirs.each { srcDir ->
                sourceSets[task.sourceSet.name][srcDir.name].srcDirs += srcDir
            }
        }
    }
}

// Assign all Java source dirs to Groovy, as the groovy compiler should take care of them.
sourceSets.each { ss ->
    ss.groovy.srcDirs += ss.java.srcDirs
    ss.java.srcDirs = []
}

dependencies {
    // grpc and protobuf
    compile 'com.google.code.gson:gson:2.7'
    compile "com.google.api.grpc:proto-google-common-protos:1.0.0"
    compile "io.grpc:grpc-alts:${grpcVersion}"
    compile "io.grpc:grpc-netty:${grpcVersion}"
    compile "io.grpc:grpc-protobuf:${grpcVersion}"
    compile "io.grpc:grpc-stub:${grpcVersion}"
    compile "io.grpc:grpc-auth:${grpcVersion}"
    compile "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    compile "io.netty:netty-tcnative-boringssl-static:${nettyTcNativeVersion}"

    // Use the latest Groovy version for building this library
    compile 'org.codehaus.groovy:groovy-all:2.4.18'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.yaml', name: 'snakeyaml', version: '1.27'
    compile group: 'com.jayway.restassured', name: 'rest-assured', version: '2.4.1'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.6.1'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.5.3'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.6.1'
    testCompile( 'com.athaydes:spock-reports:1.4.0' ) {
        transitive = false
    }
    compile group: 'com.google.protobuf', name: 'protobuf-java', version: "${protobufVersion}"
    compile group: 'com.google.protobuf', name: 'protobuf-java-util', version: "${protobufVersion}"

    // Use the Kubernetes API
    compile 'io.fabric8:kubernetes-client:4.10.2'
    compile 'io.fabric8:openshift-client:4.10.2'

    compile 'io.kubernetes:client-java:2.0.0-beta1'

    compile group: 'com.github.docker-java', name: 'docker-java', version: '3.1.0-rc-3'

    compile group: 'org.spockframework', name: 'spock-core', version: '1.2-groovy-2.4'

    compile group: 'commons-httpclient', name: 'commons-httpclient', version: '3.1'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.7'

    compile 'com.google.api.grpc:googleapis-common-protos:0.0.3'

    compile group: 'com.opencsv', name: 'opencsv', version: '4.0'

    testCompile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    compile group: 'com.codepine.api', name: 'testrail-api-java-client', version: '2.0.1'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.2'

    //jsoup for testrailupdater
    compile group: 'org.jsoup', name: 'jsoup', version: '1.12.1'

    //JavaMail for mail verifications
    compile group: 'com.sun.mail', name: 'javax.mail', version: '1.6.0'

    //Slack API
    compile group: 'com.slack.api', name: 'slack-api-client', version: '1.5.0-M1'

    // JAX-B dependencies for JDK 9+
    implementation "jakarta.xml.bind:jakarta.xml.bind-api:2.3.2"
    implementation "org.glassfish.jaxb:jaxb-runtime:2.3.2"

    // Required to make codenarc work with JDK 14.
    // See https://github.com/gradle/gradle/issues/12646.
    constraints {
        "codenarc"("org.codehaus.groovy:groovy:2.5.10")
        "codenarc"("org.codehaus.groovy:groovy-xml:2.5.10")
    }
}



tasks.withType(Test) {
    def testGroups = new TestGroups(System.getProperty("groups"))

    useJUnit {
        includeCategories testGroups.getIncludedGroups()
        excludeCategories testGroups.getExcludedGroups()
    }

    testLogging {
        showStandardStreams true
        exceptionFormat "full"
    }
}

test {
    testLogging.showStandardStreams = true

    // This ensures that repeated invocations of tests actually run the tests.
    // Otherwise, if the tests pass, Gradle "caches" the result and doesn't actually run the tests,
    // which is not the behaviour we expect of E2Es.
    // https://stackoverflow.com/questions/42175235/force-gradle-to-run-task-even-if-it-is-up-to-date/42185919
    outputs.upToDateWhen { false }
}

task updateTestRail(type: JavaExec) {
    // Set main property to name of Groovy script class.
    main = 'testrailupdater.TestRailUpdater'

    // Set classpath for running the Groovy script.
    classpath = sourceSets.main.runtimeClasspath
}
